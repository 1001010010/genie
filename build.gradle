plugins {
    id 'nebula.netflixoss' version '2.2.7'
}

ext.githubProjectName = rootProject.name

apply plugin: 'java'


allprojects {
    clean {
        delete 'genie-db'
        delete 'derby.log'
    }
}

subprojects {
    apply plugin: 'nebula.netflixoss'
    apply plugin: 'java'
    apply plugin: 'checkstyle'
    apply plugin: 'findbugs'
    apply plugin: 'pmd'
    apply plugin: 'jacoco'

    group = "com.netflix.${githubProjectName}" // set to organization of project
    
    repositories {
        jcenter()
        mavenLocal()
    }

    dependencies {
        compile 'org.apache.commons:commons-lang3:3.2.1'
        compile 'org.slf4j:slf4j-log4j12:1.7.0'

        testCompile 'junit:junit:4.11'
        testCompile 'org.mockito:mockito-core:1.9.5'
    }

    checkstyle {
        ignoreFailures = true 
        configFile = new File(project.parent.projectDir, 'codequality/checkstyle.xml')
    }

    findbugs {
        ignoreFailures = true
    }

    tasks.withType(Pmd) {
        reports.html.enabled true
        ignoreFailures = true
    }

    test {
        jacoco {
            excludes = ['com/netflix/genie/common/model/*_*']
        }
    }

    jacocoTestReport {
        doFirst {
            classDirectories = fileTree(dir: "${buildDir}/classes/main/").exclude(['com/netflix/genie/common/model/*_*'])
        }
        reports {
            xml.enabled = true
        }
    }
}
