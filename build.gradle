plugins {
    id 'nebula.netflixoss' version '2.2.7'
}

// Change if github project name is not the same as the root project's name
ext.githubProjectName = rootProject.name

apply plugin: 'java'


allprojects {
    clean {
        delete 'genie-db'
        delete 'derby.log'
    }
}

subprojects {
    apply plugin: 'nebula.netflixoss'
    apply plugin: 'java'
    apply plugin: 'checkstyle'
    apply plugin: 'findbugs'
    apply plugin: 'pmd'
    apply plugin: 'jacoco'

    group = "com.netflix.${githubProjectName}" // set to organization of project
    
    repositories {
        jcenter()
        mavenLocal()
    }

    checkstyle {
        ignoreFailures = true 
        configFile = new File(project.parent.projectDir, 'codequality/checkstyle.xml')
    }
    findbugs {
        toolVersion = '3.0.0'
        ignoreFailures = true
    }
    tasks.withType(Pmd) {
        reports.html.enabled true
        ignoreFailures = true
    }
    test {
        jacoco {
            excludes = ['com/netflix/genie/common/model/*_*']
        }
    }
    jacocoTestReport {
        doFirst {
            classDirectories = fileTree(dir: "${buildDir}/classes/main/").exclude(['com/netflix/genie/common/model/*_*'])
        }
        reports {
            xml.enabled = true
        }
    }
}
