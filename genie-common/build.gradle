ext {
    metaModelIgnorePattern = "**/com/netflix/genie/common/model/*_.java"
    generatedSourcesDir = new File("${projectDir}/src/generated")
    generatedSourcesJavaDir = new File(generatedSourcesDir, "/java")
}

sourceSets {
    main {
        java {
            srcDir generatedSourcesJavaDir
        }
    }
}

//configurations {
//    hibernateAnnotationProcessor
//}

dependencies {
    /*******************************
     * Compile Dependencies
     *******************************/

    // Hibernate Metamodel Generator Libraries
    compile("org.hibernate:hibernate-jpamodelgen")

    // Hibernate Validator Libs
    compile("org.hibernate:hibernate-validator")

    // Spring Libs
    compile("org.springframework.boot:spring-boot-starter-data-elasticsearch")
    compile("org.springframework.boot:spring-boot-starter-data-jpa")

    // Swagger Libs
    compile("io.swagger:swagger-core:${swagger_version}")

    /*******************************
     * Provided Dependencies
     *******************************/

    /*******************************
     * Runtime Dependencies
     *******************************/

    /*******************************
     * Test Dependencies
     *******************************/
}

clean {
    delete generatedSourcesDir
}

//compileJava.compileClasspath += configurations.hibernateAnnotationProcessor
compileJava {
    doFirst {
        // Java compiler won't create the directory if it doesn't already exist
        generatedSourcesJavaDir.mkdirs()
    }
    options.compilerArgs += ['-s', generatedSourcesJavaDir]
}

//Ignore the generated classes from all our style/coverage/bug finding tools
tasks.withType(Checkstyle) {
    exclude metaModelIgnorePattern
}

tasks.withType(FindBugs) {
    classes = classes.filter {
        !it.path.contains('_')
    }
}

tasks.withType(Pmd) {
    exclude metaModelIgnorePattern
}
